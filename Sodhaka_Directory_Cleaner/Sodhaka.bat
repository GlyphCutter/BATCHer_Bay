REM Sodhaka (Empty) Directory Cleaner V1.0 by GlyphCutter

@ECHO off
mode 52,36
title D.E.D. by GlyphCutter
COLOR 06
CLS

rem %~dp0 вывод текущего пути к папке работает только в батниках
rem и установка его в переменную
SET "BoxIn=%~dp0"

rem установка текущего времени в формате ЧЧ:ММ:СС отсекая милисекунды, для лог-файла
SET TIM=%time:~0,8%

rem создание лог-файла с датой и временем произведения операции
ECHO %DATE% %TIM%>DED.log

rem отображение директории из переменной /S - со всеми подкаталогами /A:D - только каталоги /B - вывод только имен файлов
rem sort /r - обратный порядок сортировки
rem 2>nul - зануление вывода ошибок (каталог не пустой) 
rem весь алгоритм сортировки заключен в общие скобки как одна команда и выводится в лог-файл
rem все обрабатываемые каталоги выводятся и подвергаются удалению без аргументов что вызывает ошибку при наличии Непустых каталогов
rem в случае если каталог был пуст и удаление прошло - выводится сообщение об удалении, если сообщение об удалении не было выведено (вследствии невозможности удаления
rem не пустого каталога ранее) выводится сообщение о пропуске
rem && следующая команда исполняется только если предыдущая исполнилась успешно (вывод сообщения об удалении после успешной операции удаления)
rem || следующая команда исполняется только если предыдущая окончилась неудачей (вывод сообщения о пропуске после неудачной операции вывода сообщения об удалении)
2>nul (FOR /F "usebackq delims=" %%f In (`Dir "%BoxIn%" /S /A:D /B ^|Sort /R`) DO echo "%%f" && rmdir "%%f" && echo EMPTY DIRECTORY.....DELETE || echo FILES FOUND.........PASS)>>DED.log

rem чтение лог-файла обратно в консольдля его отображения (кривоватая реализация, но вывести одновременно и в лог и в консоль иным образом пока не вышло)
TYPE DED.log

rem вывод сообщения о сохранении процесса операции в лог файл
ECHO.
ECHO THE PROGRESS HAS BEEN SAVED AS DED.LOG
ECHO.

pause
Exit
